# $Id$
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Isenmann <daniel@archlinux.org>

_pkgname=gimp
pkgname=${_pkgname}-nostalgia
pkgver=2.10.4
pkgrel=2
pkgdesc='GNU Image Manipulation Program'
url='https://www.gimp.org/'
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('babl' 'dbus-glib' 'desktop-file-utils' 'gegl' 'glib-networking' 'hicolor-icon-theme'
         'jasper' 'lcms' 'libheif' 'libexif' 'libgudev' 'libmng' 'libmypaint' 'librsvg' 'libwebp'
         'libwmf' 'libxmu' 'libxpm' 'mypaint-brushes' 'openexr' 'poppler-data' 'pygtk')
makedepends=('alsa-lib' 'curl' 'ghostscript' 'gtk-doc' 'intltool' 'iso-codes' 'poppler-glib')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
            'poppler-glib: for pdf support'
            'alsa-lib: for MIDI event controller module'
            'curl: for URI support'
            'ghostscript: for postscript support')
conflicts=('gimp' 'gimp-plugin-wavelet-decompose')
replaces=('gimp' 'gimp-plugin-wavelet-decompose')
provides=('gimp')
source=(https://download.gimp.org/pub/gimp/v${pkgver%.*}/${_pkgname}-${pkgver}.tar.bz2
        linux.gpl
        "legacy-tool-order.patch"
        "floating-selection-erasable.patch"
        "gradient-tool-always-instant.patch"
        "free-selection-immediate-commit.patch"
        "revert-zoom-focus-pointer.patch")
sha256sums=('ffb0768de14a2631b3d7ed71f283731441a1b48461766c23f0574dce0706f192'
            '1003bbf5fc292d0d63be44562f46506f7b2ca5729770da9d38d3bb2e8a2f36b3'
            'e7d9105aad2ce2eef4e7f4aff4e34b54604190ec95c7e6e3280f432e2f5c1f99'
            '26d78ab98d54f099133e35e0dbdd3c5cebd5190f08a007e27ef72f05d2b5d24c'
            '21e7e204d93d25e3ae88c62d5c382686e56d07097a1b5c1ebc4cac4c23154876'
            '77378f7b10f3f36af49cc1f7ca2433afb99b49fdb91de97642a8b2daaba1b84a'
            'b8cdc23aae051b185d39fc51ccb0e9bc16ee33812a054590cfbf2195b270453f')

prepare() {
  cd ${_pkgname}-${pkgver}
  _mypaintver=$(echo /usr/lib/libmypaint-*.so | grep -o -E '\-[0-9]+(\.[0-9]+)*' | head -1)
  sed -i "s|\\(libmypaint\\)\\( >= libmypaint_required_version\\)|\\1${_mypaintver}\\2|g" configure.ac
  autoreconf -vi

  # my own tweaks
  for patch in ../../*.patch; do
    patch -Np1 -i ${patch}
  done

}

build() {
pkg-config --exists --print-errors "OpenEXR >= 1.6.1"
  cd ${_pkgname}-${pkgver}
  PYTHON=/usr/bin/python2 ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/bin \
    --enable-mp \
    --enable-gimp-console \
    --enable-python \
    --enable-gtk-doc \
    --with-bug-report-url='https://bugs.archlinux.org/?string=gimp' \
    --with-openexr \
    --without-aa
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${_pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/linux.gpl" "${pkgdir}/usr/share/gimp/2.0/palettes/Linux.gpl"

  rm "${pkgdir}/usr/share/man/man1/gimp-console.1"
  ln -s gimp-console-${pkgver%.*}.1.gz "${pkgdir}/usr/share/man/man1/gimp-console.1.gz"
  ln -s gimptool-2.0 "${pkgdir}/usr/bin/gimptool"
  ln -sf gimptool-2.0.1.gz "${pkgdir}/usr/share/man/man1/gimptool.1.gz"
}

# vim: ts=2 sw=2 et:
