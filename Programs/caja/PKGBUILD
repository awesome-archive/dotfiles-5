# Maintainer: Mort Yao
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=caja
pkgver=v1.18.0.r3.g08ef602
pkgrel=1
pkgdesc="File manager for the MATE desktop"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('exempi' 'gvfs' 'libexif' 'libsm' 'mate-desktop' 'libnotify')
makedepends=('gobject-introspection' 'intltool')
optdepends=('gstreamer: automatic media playback when mouseover')
groups=('mate')
conflicts=('caja-gtk3')
replaces=('caja-gtk3')
source=("${pkgname}::git://github.com/soimort/caja.git")
sha1sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname
  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --libexecdir=/usr/lib/$pkgname \
      --with-gtk=3.0 \
      --enable-introspection \
      --disable-update-mimedb

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install

  # Remove D-Bus activation file to avoid conflict with nautilus-desktop
  rm -r "$pkgdir/usr/share/dbus-1/"
}
